class HydraulicSystem(const fluidPropagationDelay) 

signature: 

	visible: gearHandle, commandAttach, commandDetach, gearShift, gearDrive, gearPark, gearReverse, controlCommandDetach, controlCommandAttach, controlGearShift;

	temporal domain: real;
	
	domains:
		GearPosition: {Drive, Park, Reverse};
		ShiftWidth: 1..2;
		ShiftDirection: {Up, Down};

	items: 
		event gearHandle (GearPosition);
		event commandAttach;
		event commandDetach;
		event gearShift (ShiftWidth, ShiftDirection);
		event gearDrive;
		event gearPark;
		event gearReverse;
		event controlCommandDetach;
		event controlCommandAttach;
		event controlGearShift (ShiftWidth, ShiftDirection);
		
	axioms:
	vars:
		gear: GearPosition;
		gearShiftWidth: ShiftWidth;
		gearShiftDirection: ShiftDirection;
		t: real;
	formulae:		
		GearHandle: 
			(gear = Drive & gearHandle (gear) ->
				(ex t (0 < t <= fluidPropagationDelay & Futr (gearDrive, t)))) &
			(gear = Park & gearHandle (gear) ->
				(ex t (0 < t <= fluidPropagationDelay & Futr (gearPark, t)))) &
			(gear = Reverse & gearHandle (gear) ->
				(ex t (0 < t <= fluidPropagationDelay & Futr (gearReverse, t))));
		
		GearHandleEvent:
			gearDrive -> all t (0 <= t <= fluidPropagationDelay -> not Futr(gearReverse | gearPark, t)) &
			gearReverse -> all t (0 <= t <= fluidPropagationDelay -> not Futr(gearDrive | gearPark, t)) &
			gearPark -> all t (0 <= t <= fluidPropagationDelay -> not Futr(gearReverse | gearDrive, t));
		
		PropagateGearShiftCommand:
			all gearShiftWidth (all gearShiftDirection (controlGearShift (gearShiftWidth, gearShiftDirection) ->
				ex t (0 < t <= fluidPropagationDelay & Futr (gearShift (gearShiftWidth, gearShiftDirection), t))));
		
		PropagateTorqueConverterCommand:
			controlCommandAttach -> ex t (0 <= t <= fluidPropagationDelay & Futr (commandAttach, t)) &
			controlCommandDetach -> ex t (0 <= t <= fluidPropagationDelay & Futr (commandDetach, t));
			
		/*
		 * Considerando che il funzionamento del PlanetaryGearSet impedisce la
		 * selezione ripetuta della marcia attuale, si assume qui che la leva
		 * del cambio si comporti in modo coerente.
		 */

end